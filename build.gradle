apply plugin: "war"

repositories {
    maven {
        url 'http://mavenproxy.finntech.no/finntech-release/'
    }
}

configurations {
    solr
}

ext.versions = [
    solr: "4.10.4",
    log4j2: "2.1",
    jsonevent: "3.1.0-finn-1",
    slf4j: "1.7.9"
]

dependencies {
    solr "org.apache.solr:solr:${versions.solr}@war"

    compile (
        "net.logstash.log4j2:log4j2-logstash-jsonevent-layout:${versions.jsonevent}",
        "org.apache.logging.log4j:log4j-api:${versions.log4j2}",
        "org.apache.logging.log4j:log4j-core:${versions.log4j2}",
        "org.apache.logging.log4j:log4j-jcl:${versions.log4j2}",
        "org.apache.logging.log4j:log4j-1.2-api:${versions.log4j2}",
        "org.apache.logging.log4j:log4j-slf4j-impl:${versions.log4j2}",
        "org.slf4j:slf4j-api:${versions.slf4j}",
        "org.slf4j:jul-to-slf4j:${versions.slf4j}",
        "org.slf4j:jcl-over-slf4j:${versions.slf4j}",
        "com.lmax:disruptor:3.3.0"
    )
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

task unzip(type: Copy) {
    from {
        configurations.solr.collect { zipTree(it) }
    }
    into "$buildDir/rootContent"
}

defaultTasks 'war'
war.dependsOn('unzip')
war {
    from "$buildDir/rootContent"
    baseName = "solr-log4j2"
    //classifier "0"
    version "${versions.solr}"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
}
